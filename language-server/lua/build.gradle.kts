plugins {
    id("java-library")
    id("kotlin")
}


dependencies {
    // https://mvnrepository.com/artifact/org.eclipse.lsp4j/org.eclipse.lsp4j
    implementation(BuildConfig.Libs.Tools.lsp4j)
    implementation(BuildConfig.Libs.Tools.antlr_kotlin_runtime)
    // add the plugin to the classpath

}


// in antlr-kotlin-plugin <0.0.5, the configuration was applied by the plugin.
// starting from verison 0.0.5, you have to apply it manually:
tasks.register<com.strumenta.antlrkotlin.gradleplugin.AntlrKotlinTask>("generateKotlinCommonGrammarSource") {
    // the classpath used to run antlr code generation
    antlrClasspath = configurations.detachedConfiguration(
        // antlr itself
        // antlr is transitive added by antlr-kotlin-target,
        // add another dependency if you want to choose another antlr4 version (not recommended)
        // project.dependencies.create("org.antlr:antlr4:$antlrVersion"),

        // antlr target, required to create kotlin code
        project.dependencies.create(BuildConfig.Libs.Tools.antlr_kotlin_target)
    )
    maxHeapSize = "64m"
    packageName = "com.dingyi.myluaapp.editor.lsp.server.lua.antlrkotlin.generated"
    arguments = listOf("-visitor", "-package", "com.dingyi.myluaapp.editor.lsp.server.lua.common.parser")
    source = project.objects
        .sourceDirectorySet("antlr", "antlr")
        .srcDir("src/main/antlr").apply {
            include("*.g4")
        }
    // outputDirectory is required, put it into the build directory
    // if you do not want to add the generated sources to version control
    outputDirectory = File("src/gen/kotlin-antlr")
    // use this settings if you want to add the generated sources to version control
    // outputDirectory = File("src/commonAntlr/kotlin")
}


java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    sourceSets["main"].java {
        srcDirs("src/main/kotlin", "src/gen/kotlin-antlr")
    }

}